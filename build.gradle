plugins {
    id 'org.jetbrains.kotlin.jvm'       version '1.4.10' apply false
    id 'org.jmailen.kotlinter'          version '3.2.0'  apply false
}

group 'gal.usc.citius.processmining.dfgfiltering'
version '1.0'

apply plugin: 'java-library'
apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'org.jmailen.kotlinter'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

java {
    withSourcesJar()
}

test {
    useJUnitPlatform {
        includeEngines 'spek2'
    }
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation      group: "org.jetbrains.kotlin",      name: "kotlin-stdlib-jdk8"
    implementation      group: 'com.xenomachina',           name: 'kotlin-argparser',       version: '2.0.7'

    testImplementation  group: 'junit',                     name: 'junit',                  version: '4.13'
    testImplementation  group: 'org.junit.platform',        name: 'junit-platform-runner',  version: '1.6.0'
    testImplementation( group: 'org.amshove.kluent',        name: 'kluent',                 version: '1.61') {
        exclude         group: 'org.jetbrains.kotlin',      module: 'kotlin-reflect'
    }
    testImplementation( group: 'org.spekframework.spek2',   name: 'spek-dsl-jvm',           version: '2.0.9') {
        exclude         group: 'org.jetbrains.kotlin'
    }
    testRuntimeOnly(    group: 'org.spekframework.spek2',   name: 'spek-runner-junit5',     version: '2.0.9') {
        exclude         group: 'org.junit.platform'
        exclude         group: 'org.jetbrains.kotlin'
    }
    testRuntimeOnly(    group: 'org.jetbrains.kotlin',      name: 'kotlin-reflect')
}

jar {
    baseName 'EfficientDFGEdgeFiltering'
    zip64 true

    manifest {
        attributes 'Main-Class': 'gal.usc.citius.processmining.dfgfiltering.MainKt'
    }

    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() ? it : zipTree(it).matching { it.exclude("**/*.kt") }
        }
    }
}

wrapper {
    gradleVersion = "6.6.1"
}
